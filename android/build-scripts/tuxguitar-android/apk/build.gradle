apply plugin: 'com.android.application'

// different path structure for the different plugins:

// android-specific plugins
def plugins = [
	'TuxGuitar-android-gervill-plugin',
	'TuxGuitar-android-midi-plugin',
	'TuxGuitar-android-browser-saf-plugin',
]
// plugins shared with desktop app, without unit tests
def plugins_common = [
		'TuxGuitar-gtp',
		'TuxGuitar-gpx',
		'TuxGuitar-ptb',
		'TuxGuitar-midi',
		'TuxGuitar-lilypond',
		'TuxGuitar-pdf',
		'TuxGuitar-ascii',
]
// plugins shared with desktop app, and including unit tests are handled on case-by-case basis
// see below

android {

	namespace = 'org.herac.tuxguitar.android.application'

	compileSdkVersion 31

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	defaultConfig {
		applicationId "org.herac.tuxguitar.android.application"
		minSdkVersion 24
		targetSdkVersion 33
		multiDexEnabled true
		versionCode Integer.parseInt(new Date().format('yyMMddHH'))
		versionName "9.99-SNAPSHOT"
		setProperty("archivesBaseName", "tuxguitar-android-9.99-SNAPSHOT")
	}

	buildTypes {
		release {
			zipAlignEnabled true
		}
	}

	sourceSets {
		main {
			java.srcDirs = ['./build/generated/tuxguitar-android-apk-src/src']
			resources.srcDirs = ['./build/generated/tuxguitar-android-apk-src/share']
		}
	}
}

task generateSources {
 	// copy android-specific plugins to tmp folder
	plugins.each { String plugin ->
		copy {
			from '../../../' + plugin
			into './build/generated/tuxguitar-android-apk-src'

			exclude '**/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin'
		}
	}
	// copy to tmp folder plugins shared with desktop app, without unit tests
	plugins_common.each { String plugin ->
		copy {
			from '../../../../common/' + plugin
			into './build/generated/tuxguitar-android-apk-src'

			exclude '**/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin'
		}
	}
	// specific case: plugin shared with desktop app, including unit tests
	copy {
		from '../../../../common/TuxGuitar-compat/share/META-INF/tuxguitar-compat.info'
		into './build/generated/tuxguitar-android-apk-src/share/META-INF/'
	}
	copy {
		from '../../../../common/TuxGuitar-compat/src/main/java'
		into './build/generated/tuxguitar-android-apk-src/src'
	}

	// create unified /META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin file
	List<String> fileNames = new ArrayList<String>()
	// android-specific plugins
	plugins.each { String plugin ->
		fileNames.add('../../../' + plugin + '/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin')
	}
	// plugins shared with desktop app, without unit tests
	plugins_common.each { String plugin ->
		fileNames.add('../../../../common/' + plugin + '/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin')
	}
	// plugins shared with desktop app, and including unit tests
	fileNames.add('../../../../common/TuxGuitar-compat/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin')

	File file = new File('apk/build/generated/tuxguitar-android-apk-src/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin');
 	file.getParentFile().mkdirs()
 	file.text = files(fileNames).collect{it.getText()}.join("\n")
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation project(':tuxguitar-android')
	implementation project(':tuxguitar-android-resources')
	implementation project(':tuxguitar-android-gervill')
	implementation project(':tuxguitar-android-midi')
	implementation project(':tuxguitar-android-browser-saf')
	implementation project(':tuxguitar-android-gervill-soundbank')

	implementation 'com.itextpdf:itextpdf:5.5.13.3'
	implementation 'com.itextpdf.tool:xmlworker:5.5.13.1'
	implementation 'org.apache.commons:commons-compress:1.26.0'
	implementation 'commons-io:commons-io:2.16.1'
	implementation 'org.apache.commons:commons-lang3:3.17.0'
}
